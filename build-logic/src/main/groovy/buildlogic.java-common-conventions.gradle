plugins {
    id 'java-library'
    id 'com.github.ben-manes.versions'
}

repositories {
    maven {
        name = 'Jitpack'
        url = 'https://jitpack.io/'

        content {
            includeGroupByRegex 'com\\.github\\.[a-zA-Z0-9_-]+'
        }
    }

    maven {
        name = 'Akazukin Repository - Public Library'
        url = 'https://maven.akazukin.org/refer/maven-public-libraries/'

        content {
            includeGroup 'org.akazukin'
        }
    }

    mavenCentral()
    mavenLocal()
}


def javaVersion = properties.getOrDefault('java.version', 8) as int
def javaCompatSource = properties.getOrDefault('java.compatibility.source', javaVersion) as int
def javaCompatTarget = properties.getOrDefault('java.compatibility.target', javaCompatSource) as int

tasks.withType(JavaCompile).configureEach {
    configure(options) {
        encoding = 'UTF-8'
        compilerArgs << '-Xlint'
        compilerArgs << '-Xmaxerrs' << '10000'
        compilerArgs << '-Xmaxwarns' << '10000'
        compilerArgs << '-Xdiags:verbose'
    }
    it.sourceCompatibility = javaCompatSource
    it.targetCompatibility = javaCompatTarget
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(javaVersion))


tasks.withType(Javadoc).configureEach {
    configure(options) {
        locale = 'en_US'
        encoding = docEncoding = charSet = 'UTF-8'
    }
}


tasks.compileJava.dependsOn(processResources)

tasks.withType(AbstractCopyTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
