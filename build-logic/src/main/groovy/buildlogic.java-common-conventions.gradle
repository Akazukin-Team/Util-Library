plugins {
    id 'java-library'
    id 'com.github.ben-manes.versions'
}

repositories {
    maven {
        name = 'Jitpack'
        url = 'https://jitpack.io/'
    }
    maven {
        name = 'Akazukin Repository'
        url = 'https://maven.akazukin.org/refer/maven-public/'
    }
    mavenCentral()
}


def javaVersion = properties.getOrDefault('java_version', 8) as int

tasks.withType(JavaCompile).configureEach {
    configure(options) {
        encoding = 'UTF-8'
        compilerArgs << '-Xlint'
        compilerArgs << '-Xmaxerrs' << '10000'
        compilerArgs << '-Xmaxwarns' << '10000'
        compilerArgs << '-Xdiags:verbose'
    }
    it.sourceCompatibility = it.targetCompatibility = javaVersion
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(javaVersion))


tasks.withType(Javadoc).configureEach {
    configure(options) {
        locale = 'en_US'
        encoding = docEncoding = charSet = 'UTF-8'
    }
}


tasks.compileJava.dependsOn(processResources)

tasks.withType(AbstractCopyTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
