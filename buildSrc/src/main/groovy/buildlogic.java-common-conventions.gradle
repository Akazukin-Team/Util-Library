plugins {
    id 'java-library'
}

configurations {
    include
    implementation.extendsFrom(include)
    compileOnly.extendsFrom(pluginImpl)
}

repositories {
    maven {
        name = "Jitpack"
        url = "https://jitpack.io/"
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint'
    options.compilerArgs << '-Xmaxerrs' << '10000'
    options.compilerArgs << '-Xmaxwarns' << '10000'
    options.compilerArgs << '-Xdiags:verbose'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(properties.getOrDefault("java_version", 8) as int)
compileJava.sourceCompatibility = compileJava.targetCompatibility = properties.getOrDefault("java_version", 8)

tasks.jar.dependsOn(processResources)

java {
    withSourcesJar()
}

tasks.withType(AbstractCopyTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
