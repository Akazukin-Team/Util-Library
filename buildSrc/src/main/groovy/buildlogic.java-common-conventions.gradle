import java.nio.charset.StandardCharsets

plugins {
    id 'java-library'
    id "com.github.ben-manes.versions"
}

int javaVersion = 8

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    maven {
        name = "Jitpack"
        url = "https://jitpack.io/"
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform
}

tasks.withType(JavaCompile).configureEach {
    configure(options) {
        encoding = StandardCharsets.UTF_8.displayName()

        compilerArgs << '-Xlint'
        compilerArgs << '-Xmaxerrs' << '10000'
        compilerArgs << '-Xmaxwarns' << '10000'
        compilerArgs << '-Xdiags:verbose'
    }

    it.sourceCompatibility = it.targetCompatibility = javaVersion
}

tasks.withType(Javadoc).configureEach {
    configure(options) {
        encoding = docEncoding = charSet = StandardCharsets.UTF_8.displayName()
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(AbstractCopyTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
