import java.nio.charset.StandardCharsets

plugins {
    id 'java-library'
    id "com.github.ben-manes.versions"
}

int javaVersion = 8

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    maven {
        name = "Jitpack"
        url = "https://jitpack.io/"
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform
}

String encoding = StandardCharsets.UTF_8.displayName()

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = encoding

    it.options.compilerArgs << '-Xlint'
    it.options.compilerArgs << '-Xmaxerrs' << '10000'
    it.options.compilerArgs << '-Xmaxwarns' << '10000'
    it.options.compilerArgs << '-Xdiags:verbose'

    it.sourceCompatibility = it.targetCompatibility = javaVersion
}

tasks.withType(Javadoc).configureEach {
    it.options.encoding = (it.options as StandardJavadocDocletOptions).docEncoding = (it.options as StandardJavadocDocletOptions).charSet = encoding
}

java.toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(AbstractCopyTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
