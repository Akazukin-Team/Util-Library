plugins {
    id 'buildlogic.java-common-conventions'
    id 'maven-publish'
    id 'java-library'
}

configurations {
    include
    implementation.extendsFrom(include)
}

java {
    withSourcesJar()
    //withJavadocJar()
}

Properties props = new Properties()
try (FileInputStream fis = new FileInputStream(new File(parent.rootDir.getPath(), "application.properties"))) {
    props.load(fis)
}

publishing {
    repositories {
        maven {
            name = "GitHub"
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
            credentials {
                username = System.getenv("GITHUB_REPOSITORY_OWNER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "GitLab"
            url = uri("https://gitlab.com/api/v4/projects/${System.getenv("CI_PROJECT_ID")}/packages/maven")
            credentials(HttpHeaderCredentials) {
                name = "Job-Token"
                value = System.getenv("CI_JOB_TOKEN")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.artifact
            version = project.version

            pom {
                name = props.getProperty("name")
                description = props.getProperty("description")
                url = props.getProperty("url")

                organization {
                    name = props.getProperty("vendor")
                    url = props.getProperty("vendor-url")
                }

                developers {
                    developer {
                        id = 'currypan1229'
                        name = 'Currypan1229'
                    }
                }
            }
        }
    }
}
